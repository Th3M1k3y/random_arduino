uint8_t dataPin  = 11;    // Yellow wire on Adafruit Pixels
uint8_t clockPin = 12;    // Green wire on Adafruit Pixels

int leds = 25;
int dmxStartChan = 56;

#include <DMXSerial.h>
#include "Adafruit_WS2801.h"
Adafruit_WS2801 strip = Adafruit_WS2801(leds, dataPin, clockPin);

int dmxChannels = 0;
unsigned long lastUpdate = 0;

void setup()
{
  DMXSerial.init(DMXReceiver);
  dmxChannels = leds * 3;
  strip.begin();
  pinMode(LED_BUILTIN, OUTPUT);
  
  pinMode(2, OUTPUT);
  digitalWrite(2, HIGH); // Enable DMX

  allLED(Color(255, 255, 255));
  delay(100);
  allLED(Color(0, 0, 0));
}

void loop()
{
  if (DMXSerial.dataUpdated())
  {
    DMXSerial.resetUpdated();
    lastUpdate = millis();
    digitalWrite(LED_BUILTIN, !digitalRead(LED_BUILTIN));
    
    for (int i = 0; i < leds; i++)
    {
      int chan = (3 * i) + dmxStartChan;
      strip.setPixelColor(i, Color(DMXSerial.read(chan), DMXSerial.read(chan + 1), DMXSerial.read(chan + 2)));
    }
    strip.show();
  }

  if (millis() - lastUpdate >= 5000)
  {
    allLED(Color(0, 0, 0));
    lastUpdate = millis();
  }
}

uint32_t Color(byte r, byte g, byte b)
{
  uint32_t c;
  c = r;
  c <<= 8;
  c |= g;
  c <<= 8;
  c |= b;
  return c;
}


void allLED(uint32_t c)
{
  for (int i = 0; i < leds; i++)
  {
    strip.setPixelColor(i, c);
  }
  strip.show();
}
